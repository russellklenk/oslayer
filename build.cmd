@ECHO OFF

SET VSVERSION_2013=12.0
SET VSVERSION_2015=14.0
SET VSVERSION=%VSVERSION_2015%

IF NOT DEFINED DevEnvDir (
    CALL "C:\Program Files (x86)\Microsoft Visual Studio %VSVERSION%\VC\vcvarsall.bat" x86_amd64
)

SET OUTPUTDIR="%~dp0build"
SET INCLUDES=-I%VULKAN_SDK%\Include -I..\include -I..\src

SET DEFINES_DEBUG=/D WINVER=0x0601 /D _WIN32_WINNT=0x0601 /D DEBUG /D _DEBUG /D UNICODE /D _UNICODE /D BUILD_STATIC /D _STDC_FORMAT_MACROS /D VK_NO_PROTOTYPES /D VK_USE_PLATFORM_WIN32_KHR
SET CPPFLAGS_DEBUG=%INCLUDES% /FC /nologo /W4 /WX /wd4505 /Zi /EHsc /Od
SET LINKFLAGS_DEBUG=/MTd

SET DEFINES_RELEASE=/D WINVER=0x0601 /D _WIN32_WINNT=0x0601 /D UNICODE /D _UNICODE /D BUILD_STATIC /D _STDC_FORMAT_MACROS /D VK_NO_PROTOTYPES /D VK_USE_PLATFORM_WIN32_KHR
SET CPPFLAGS_RELEASE=%INCLUDES% /FC /nologo /W4 /WX /wd4505 /Zi /EHsc /Ob2it
SET LINKFLAGS_RELEASE=/MT

SET LIBRARIES=User32.lib Gdi32.lib Shell32.lib Advapi32.lib Ole32.lib winmm.lib

IF [%1] NEQ [] (
    IF "%1" == "debug" (
        SET DEFINES=%DEFINES_DEBUG%
        SET CPPFLAGS=%CPPFLAGS_DEBUG%
        SET LNKFLAGS=%LINKFLAGS_DEBUG%
        @ECHO Building debug configuration...
    ) ELSE (
        SET DEFINES=%DEFINES_RELEASE%
        SET CPPFLAGS=%CPPFLAGS_RELEASE%
        SET LNKFLAGS=%LINKFLAGS_RELEASE%
        @ECHO Building release configuration - did you mean to specify "debug"?
    )
) ELSE (
    SET DEFINES=%DEFINES_RELEASE%
    SET CPPFLAGS=%CPPFLAGS_RELEASE%
    SET LNKFLAGS=%LINKFLAGS_RELEASE%
    @ECHO Building release configuration...
)

IF NOT EXIST %OUTPUTDIR% mkdir %OUTPUTDIR%

XCOPY %VULKAN_SDK%\Source\lib\vulkan-1.dll %OUTPUTDIR% /Y /Q
XCOPY %VULKAN_SDK%\Source\lib\vulkan-1.pdb %OUTPUTDIR% /Y /Q

PUSHD %OUTPUTDIR%
cl %CPPFLAGS% ..\main.cc %DEFINES% %LIBRARIES% %LNKFLAGS% /Feoslayer.exe
cl %CPPFLAGS% ..\threadpool.cc %DEFINES% %LIBRARIES% %LNKFLAGS% /Fethreadpool.exe
cl %CPPFLAGS% ..\scheduler.cc %DEFINES% %LIBRARIES% %LNKFLAGS% /Fescheduler.exe
cl %CPPFLAGS% ..\vulkan.cc %DEFINES% %LIBRARIES% %LNKFLAGS% /Fevulkan.exe
cl %CPPFLAGS% ..\audio.cc %DEFINES% %LIBRARIES% %LNKFLAGS% /Feaudio.exe
cl %CPPFLAGS% ..\filepath.cc %DEFINES% %LIBRARIES% %LNKFLAGS% /Fefilepath.exe
POPD

@ECHO Build complete.

