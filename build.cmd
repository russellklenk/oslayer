@ECHO OFF

SET VSVERSION_2013=12.0
SET VSVERSION_2015=14.0
SET VSVERSION=%VSVERSION_2015%

IF NOT DEFINED DevEnvDir (
    CALL "C:\Program Files (x86)\Microsoft Visual Studio %VSVERSION%\VC\vcvarsall.bat" x86_amd64
)

SET OUTPUTDIR="%~dp0build"
SET INCLUDES=-I..\include -I..\src

SET DEFINES_DEBUG=/D WINVER=0x0601 /D _WIN32_WINNT=0x0601 /D DEBUG /D _DEBUG /D UNICODE /D _UNICODE /D BUILD_STATIC /D _STDC_FORMAT_MACROS
SET CPPFLAGS_DEBUG=%INCLUDES% /FC /nologo /W4 /WX /wd4505 /Zi /EHsc /Od
SET LINKFLAGS_DEBUG=/MTd

SET DEFINES_RELEASE=/D WINVER=0x0601 /D _WIN32_WINNT=0x0601 /D UNICODE /D _UNICODE /D BUILD_STATIC /D _STDC_FORMAT_MACROS
SET CPPFLAGS_RELEASE=%INCLUDES% /FC /nologo /W4 /WX /wd4505 /Zi /EHsc /Ob2it
SET LINKFLAGS_RELEASE=/MT

SET LIBRARIES=User32.lib Gdi32.lib Shell32.lib Advapi32.lib winmm.lib

IF [%1] NEQ [] (
    IF "%1" == "debug" (
        SET DEFINES=%DEFINES_DEBUG%
        SET CPPFLAGS=%CPPFLAGS_DEBUG%
        SET LNKFLAGS=%LINKFLAGS_DEBUG%
        @ECHO Building debug configuration...
    ) ELSE (
        SET DEFINES=%DEFINES_RELEASE%
        SET CPPFLAGS=%CPPFLAGS_RELEASE%
        SET LNKFLAGS=%LINKFLAGS_RELEASE%
        @ECHO Building release configuration - did you mean to specify "debug"?
    )
) ELSE (
    SET DEFINES=%DEFINES_RELEASE%
    SET CPPFLAGS=%CPPFLAGS_RELEASE%
    SET LNKFLAGS=%LINKFLAGS_RELEASE%
    @ECHO Building release configuration...
)

IF NOT EXIST %OUTPUTDIR% mkdir %OUTPUTDIR%

PUSHD %OUTPUTDIR%
cl %CPPFLAGS% ..\main.cc %DEFINES% %LIBRARIES% %LNKFLAGS% /Feoslayer.exe
POPD

@ECHO Build complete.

